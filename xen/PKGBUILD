# Maintainer: Sam Mulvey (Refutationalist) <archlinux@sammulvey.com>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Chris Chapman (cman) <chris.chapman@aggiemail.usu.edu>

# Build Options
_build_stubdom=${build_stubdom:-false}
_boot_dir=${boot_dir:-/boot}
_efi_dir=${efi_dir:-/boot}
_efi_mountpoint=${efi_mountpoint:-/boot}

# External files used by Xen
# Check http://xenbits.xen.org/xen-extfiles/ for updates
_gmp=4.3.2
_grub=0.97
_lwip=1.3.0
_newlib=1.16.0
_pciutils=2.2.9
_polarssl=1.1.4
_tpm_emulator=0.7.4
_zlib=1.2.3


# Flags passed to make
_common_make_flags=(
	"BOOT_DIR=${_boot_dir}"
	"EFI_DIR=${_efi_dir}"
	"EFI_MOUNTPOINT=${_efi_mountpoint}"
	"XEN_VENDORVERSION=-${pkgrel}arch"
)

pkgbase=xen
pkgname=("xen" "xen-docs")
pkgver=4.18.1pre
_branch="stable-4.18"
pkgrel=1
pkgdesc='Open-source type-1 or baremetal hypervisor - stable branch'
arch=('x86_64')
url='https://xenproject.org/'
license=('GPL2')
options=(!buildflags)

makedepends=(
	'acpica'
	'bison'
	'bridge-utils'
	'fig2dev'
	'flex'
	'gettext'
	'git'
	'git'
	'glib2'
	'gnutls'
	'inetutils'
	'iproute2'
	'lib32-glibc'
	'libaio'
	'libuuid.so'
	'libx11'
	'lzo'
	'nasm'
	'ncurses'
	'openssl'
	'pandoc'
	'pciutils'
	'pixman'
	'pkgconf'
	'python'
	'sdl2'
	'systemd'
	'systemd-libs'
	'valgrind'
	'vde2'
	'wget'
	'yajl'
	'zlib'
) # last line from namcap, these depends are the xen depends
_stubdom_makedepends=('cmake')

optdepends=(
	'xen-qemu: needed for PV and HVM domUs'
	'xen-pvhgrub: bootloader for PVH domains'
)

_source=(
	"git+https://xenbits.xen.org/git-http/xen.git#branch=${_branch}"
	"efi-xen.cfg"
	"xen.conf"
	"tmpfiles.conf"
	"xen-ucode-extract.sh"
	"xen-intel-ucode.hook"
	"xen-amd-ucode.hook"
	"config"
)

# Follow the Xen securite mailing lists, and if a patch is applicable to our package
# add the URL here.
# NOTE: Patch order is important.
_patches=(
)


# (Modified) patches pulled from down stream that are useful for our package.
_feature_patches=(
	# Patches taken from Qubes' patchset.
	"0201-EFI-early-Add-noexit-to-inhibit-calling-ExitBootServ.patch"
	"0202-efi-Ensure-incorrectly-typed-runtime-services-get-ma.patch"
	"0203-Add-xen.cfg-options-for-mapbs-and-noexitboot.patch"
	"0604-libxl-create-writable-error-xenstore-dir.patch"
	"0605-libxl-do-not-wait-for-backend-on-PCI-remove-when-bac.patch"
	"0606-libxl-do-not-fail-device-removal-if-backend-domain-i.patch"
	"0607-libvchan-use-xengntshr_unshare-instead-of-munmap-dir.patch"
	"0608-x86-time-Don-t-use-EFI-s-GetTime-call-by-default.patch"
	"0609-libxl-automatically-enable-gfx_passthru-if-IGD-is-as.patch"
	"0611-libxl-do-not-require-filling-backend_domid-to-remove.patch"
	"0612-libxl-add-pcidevs-to-stubdomain-earlier.patch"
	"0613-Fix-buildid-alignment.patch"
	"0614-vchan-socket-proxy-add-reconnect-marker-support.patch"
	"0615-tools-libxl-enable-in-band-reconnect-marker-for-stub.patch"
	"0616-libxl-Add-a-utility-function-for-domain-resume.patch"
	"0617-libxl-Add-utility-function-to-check-guest-status.patch"
	"0618-libxl-Properly-suspend-stubdomains.patch"
	"0619-libxl-Fix-race-condition-in-domain-suspension.patch"
	"0620-libxl-Add-additional-domain-suspend-resume-logs.patch"
	"0621-libxl-workaround-for-Windows-PV-drivers-removing-con.patch"
	"0622-libxl-check-control-feature-before-issuing-pvcontrol.patch"
	"0624-libxl-do-not-start-qemu-in-dom0-just-for-extra-conso.patch"
	"0625-libxl-Allow-stubdomain-to-control-interupts-of-PCI-d.patch"
	"0626-Validate-EFI-memory-descriptors.patch"
	"0630-Drop-ELF-notes-from-non-EFI-binary-too.patch"
	"0643-cpufreq-enable-HWP-by-default.patch"
	"0651-x86-msi-passthrough-all-MSI-X-vector-ctrl-writes-to-.patch"
	"0652-x86-hvm-Allow-writes-to-registers-on-the-same-page-a.patch"
	"1002-libxl-do-not-start-dom0-qemu-when-not-needed.patch"
	"1006-libxl-use-EHCI-for-providing-tablet-USB-device.patch"
	"1007-libxl-allow-kernel-cmdline-without-kernel-if-stubdom.patch"
	"1008-libxl-Force-emulating-readonly-disks-as-SCSI.patch"
	"1009-tools-xenconsole-replace-ESC-char-on-xenconsole-outp.patch"
	"1010-tools-xenconsole-Don-t-replace-ESC-char-by-default.patch"
	"1014-Additional-support-in-ACPI-builder-to-support-SLIC-a.patch"
	"1016-gnttab-disable-grant-tables-v2-by-default.patch"
	"1018-Fix-IGD-passthrough-with-linux-stubdomain.patch"
	"1301-xenconsoled-install-xenstore-watch-for-all-supported.patch"
	"1302-xenconsoled-add-support-for-consoles-using-state-xen.patch"
	"1303-xenconsoled-make-console_type-use_gnttab-less-confus.patch"
	"1304-xenconsoled-add-support-for-up-to-3-secondary-consol.patch"
	"1305-xenconsoled-deduplicate-error-handling.patch"
	"1306-libxl-use-xenconsoled-even-for-multiple-stubdomain-s.patch"

	# Patches taken from XenServer's patchset.
	"0001-x86-vpt-fix-injection-to-remote-vCPU.patch"
	"0002-intel-Workaround-Nehalem-and-Westmere-C-states-bugs.patch"
	"0003-vtd-quirks-Ignore-HP-Gen8-GPU-audio-devices-for-RMRR.patch"
	"0004-pci-Add-PCI-phantom-functions-to-Xen-based-on-PCI-ve.patch"
	"0005-x86-hpet-Pre-cleanup.patch"
	"0006-x86-hpet-Use-singe-apic-vector-rather-than-irq_descs.patch"
	"0007-x86-hpet-Post-cleanup.patch"
	"0008-libxc-retry-shadow-ops-if-EBUSY-is-returned.patch"
)


# Sources required for building stubdom
_stubdom_source=(
	"vtpm-gcc12-fixes.patch"  # based on above patch
	"add-stubdom-fixes.patch" # add above patch to build
	"http://xenbits.xen.org/xen-extfiles/gmp-$_gmp.tar.bz2"
	"http://xenbits.xen.org/xen-extfiles/grub-$_grub.tar.gz"
	"http://xenbits.xen.org/xen-extfiles/lwip-$_lwip.tar.gz"
	"http://xenbits.xen.org/xen-extfiles/newlib-$_newlib.tar.gz"
	"http://xenbits.xen.org/xen-extfiles/pciutils-$_pciutils.tar.bz2"
	"http://xenbits.xen.org/xen-extfiles/polarssl-$_polarssl-gpl.tgz"
	"http://xenbits.xen.org/xen-extfiles/tpm_emulator-$_tpm_emulator.tar.gz"
	"http://xenbits.xen.org/xen-extfiles/zlib-$_zlib.tar.gz"
)


# from cheap hack known as break_out_sums.sh
_sha512sums=(
	"SKIP"
	"1bbcbcd9fb8344a207409ec9f0064a45b726416f043f902ca587f5e4fa58497a759be4ffd584fa32318e960aa478864cc05ec026c444e8d27ca8e3248bd67420" # efi-xen.cfg
	"ccaa2ff82e4203b11e5dec9aeccac2e165721d8067e0094603ecaa7a70b78c9eb9e2287a32687883d26b6ceae6f8d2ad7636ddf949eb658637b3ceaa6999711b" # xen.conf
	"53ba61587cc2e84044e935531ed161e22c36d9e90b43cab7b8e63bcc531deeefacca301b5dff39ce89210f06f1d1e4f4f5cf49d658ed5d9038c707e3c95c66ef" # tmpfiles.conf
	"a9230ec6ef9636ac3f3e4b72b1747ee8c4648a8bf4bd8dc3650365e34f1f67474429dbdd24996907d277b0ff5f235574643e781cb3ff37da954e899ddadbe0d6" # xen-ucode-extract.sh
	"7a832de9b35f4b77ee80d33310b23886f4d48d1d42c3d6ef6f8e2b428bec7332a285336864b61cfa01d9a14c2023674015beb7527bd5849b069f2be88e6500cd" # xen-intel-ucode.hook
	"99921b94a29fa7988c7fb5c17da8e598e777c972d6cae8c8643c991e5ff911a25525345ea8913945313d5c49fecf9da8cc3b83d47ab03928341e917b304370a9" # xen-amd-ucode.hook
	"8ab84146c75f436f488fa22d05925230215c258c11cad8ab10c1f0142d628ecd876fd9d6124c18e4a406166afe59bfdedb3829d557e0361d2475a26f434cd0fb" # config
)


_patch_sums=(
)


_feature_patch_sums=(
	"11e37c6f2b2d8356d4a6fea2ae452b6674eca1ac793cfedf00ae350705f34b117fe72e76c519953c0190524332c406698eb85daa96408bebcd12a497d1f61d79" # 0201-EFI-early-Add-noexit-to-inhibit-calling-ExitBootServ.patch
	"884f7f050085b6cc1c73d7ccbb6f946447c6fb09680686238630fa8b7dc7a68045836c7f60bdf5c8d3f938ab1f3d3182e92d02e2b9f2ed1c9bbfdd76ef6d0667" # 0202-efi-Ensure-incorrectly-typed-runtime-services-get-ma.patch
	"d252beeb794477aa5d88cd0607eabb903f89f54465a0adf47f03cb95476b605ec5136b5e8aabd91af165af887ec68eb52f7190a3c7c929076e18a5f5fd58ce15" # 0203-Add-xen.cfg-options-for-mapbs-and-noexitboot.patch
	"0d59f8da999b9ad42cd82880f6dab6efff4452f7e1b8f4d22e966848dd96fccc34cc1349a1e83aa4c32acd48ad482ef99fe6a8a14e139a3496e5f9132c4c40be" # 0604-libxl-create-writable-error-xenstore-dir.patch
	"0ee82e4113e9f56292c2d61b3ae157e45b293ced919658504325419c4fb3090a2ac4aa895bd33162c5d2110922db99b2d2949605d009147996ec090d5984cbb8" # 0605-libxl-do-not-wait-for-backend-on-PCI-remove-when-bac.patch
	"fff225a84d8b135fcb46445b3a26b82fed6b4826d72d658e890a6ecb825ce3513c9ff75cd84e15d09b3782e4a02ab43a0f5ba189ec4a297a1725fe1913e0e35b" # 0606-libxl-do-not-fail-device-removal-if-backend-domain-i.patch
	"c7548bc76b6f08e71a2df3083255e522a993812dc3568823a443b76db34b63625f65eb54837759ba0ce5d08a5aca10cad59c38e9fe4953ffbafaa5ce81ac1a55" # 0607-libvchan-use-xengntshr_unshare-instead-of-munmap-dir.patch
	"7bad18013917be286c447d43d6bca2893ecba2e9f9ea33227515d7bdd1c97bdbd27cfdc187db8d8f782f72e4c89231b9e1f7d4d08a5c33c92b30598d426cf8ce" # 0608-x86-time-Don-t-use-EFI-s-GetTime-call-by-default.patch
	"c83af1cdf296f05b7deef7fdb6ae4c44075c2c92bf1a0703f4963646cd252d0490171705573b1b41c9416ea4e53a21f02573d6511240e763b0f25f3e9e63211e" # 0609-libxl-automatically-enable-gfx_passthru-if-IGD-is-as.patch
	"fede52636f5a3a653f51d5d9f65dcd17f11ff4d409a1a4df4dd8443cd59510eb3c6424c492bd8253f8ee60d883b92650baa0ec0e3e9104e63a9f94338df25d7a" # 0611-libxl-do-not-require-filling-backend_domid-to-remove.patch
	"b98a862c34efd2257e99164c04a5a49b85b05544f100f4eaa8e57a0e58890bdbb122030187a4b3db8173beb50ebda1d541f203b34d6482aec5ee61b583bcddcf" # 0612-libxl-add-pcidevs-to-stubdomain-earlier.patch
	"807923061899007ea5eb08f445ae6bcf35b07e44a3b6644f4ba05513819ce72d34e1824f2316019bc1688c1e9ddaa4e6512d9940641f04e2e63e1087a527b210" # 0613-Fix-buildid-alignment.patch
	"9e8e2af77de1f2154078ffa2f55049230a7c38ffc14ba1859bb08d1b34a81c21d18a6ac7b4cc65bdabc56ee8b84ba5a3dbe4451fe79a9049223bda6c6b1fbe74" # 0614-vchan-socket-proxy-add-reconnect-marker-support.patch
	"205e4b6fd3d94fae07079a47f9fcaa92cb0a97beba4fa64b3e5e4c00d50aa20689ff4c6226f65a81547217d7f0824c38afb204e451ac7360d028ec3cbc140942" # 0615-tools-libxl-enable-in-band-reconnect-marker-for-stub.patch
	"653732bb05d359118e16fa2fdf5637d49493563ad606e467e9e38afdd796382d2c07e671481850c317a1ee04daf9198df248b6d7010a831c8815c33cf41f1ac1" # 0616-libxl-Add-a-utility-function-for-domain-resume.patch
	"3a6269b4b17502ae083d2e211ea7a2edfd1f4a333ce0744354f2ccd3acf8ff20eab56c1fd1278ed07fcfa7b0b954bdaac308f789e344be1a2e9bde1c64691f63" # 0617-libxl-Add-utility-function-to-check-guest-status.patch
	"e02dcaaef6ed5028e708c4a4eae4baf79a37a2ae70f1d760a91b40874c4959c21e5ea76af30059eab5c203b0ae34fb4f6a470cf81090167821358a532df9eb4e" # 0618-libxl-Properly-suspend-stubdomains.patch
	"06840486624986b1c096b12436e5225a4f7b19c9831777151a60d64ec59e372bc75dd1a7068dcea2826ae4d43ca160104db0dba42c346b7da09453163d0c57fc" # 0619-libxl-Fix-race-condition-in-domain-suspension.patch
	"04f0c48d916d201e68c2203fd6ac50857ec8ddc99a0e60754ebe6b22508e45fae9b21725476dfc1639211729aa108f5c49efa79860d6f31d415d89dc5b408634" # 0620-libxl-Add-additional-domain-suspend-resume-logs.patch
	"31f88b392e8c04929ee63e2b067f93fa015a2c52d0731356f5888a3ab878f2c9c9eb1d21e4fb26a32c7d93c458c7049afac3991a3c6ef34c96f218183da84192" # 0621-libxl-workaround-for-Windows-PV-drivers-removing-con.patch
	"420eb0da3f241308c787c97c2c5404a16560d83efb6d985c5abd7fc6073917931ed5eca74444f195329202652c559a846e46238d30a2aeafd456b4cdeb0312ff" # 0622-libxl-check-control-feature-before-issuing-pvcontrol.patch
	"a2fbe50d9dafb34d267a30f09e4afcc63d63751026a955323a2073a0e3551e08ede105c0c2cce9b5724569385249aae894130fd7e0a52b06a3e7e814ef32a59d" # 0624-libxl-do-not-start-qemu-in-dom0-just-for-extra-conso.patch
	"03893d596b384796c521e53ab66380b0503a53c0e9bbeeb1c9988508f45eb07b24750dc49c3f0ff65a9e649da492a18020fa67002b62adfb69bb522af7522291" # 0625-libxl-Allow-stubdomain-to-control-interupts-of-PCI-d.patch
	"e2105aa4f07bc3b9cf2b39ee0dc4e72d1dc3fc99212c126ba2889d79cc07c04ed0d33a0edb405c7ab48575fe225893c1c916ae808cd94dba00a910fd94c15ee8" # 0626-Validate-EFI-memory-descriptors.patch
	"5e7ebb5ec7ea27b236707b0c761f52a9502c540471989d28dfb577cfadd9e995c09ae6baaae52fc76cd08afba4d04f241483f6c07a501ba445ecfdaa14d0c79e" # 0630-Drop-ELF-notes-from-non-EFI-binary-too.patch
	"ae1be4d3fc8b42210cdf27d383411136e77b1e1009ece8166f081f01d04f80004387b2e30566d3f57817b176aed2d421181804e6160fea3866c000e52f135870" # 0643-cpufreq-enable-HWP-by-default.patch
	"7ec27a84ef901d07700a846135f4f56cd7683989d19b6496cad5eda77705d529367cc915bb77dd10623d0315718d42f15efa701699906c184eaf75995f108a32" # 0651-x86-msi-passthrough-all-MSI-X-vector-ctrl-writes-to-.patch
	"7af1939e38d42bc52eb03a5c12143e25bf04949821b30a2a6f098c9d4c2e5c04d621418abe275a0cc38bb92ebd3f6671641f271f4c7130fdb45aec09b732c566" # 0652-x86-hvm-Allow-writes-to-registers-on-the-same-page-a.patch
	"370d44cbc801d0e814dbd4413ea2e42424a880b5084f9876937b281eb73f6eb46a4807ea765d30d539b7ec41e3eda5fb18aa7f8ca506c32c18c359a91fd80fcf" # 1002-libxl-do-not-start-dom0-qemu-when-not-needed.patch
	"bbed2c6f3bd333cf9f65cb52315740ce27a31eb45356c49485f44650b9d3f84d45ca1465e813ffe9b3ba5cbdb8e3598da7304a139fb2490db19d2f95e96fc000" # 1006-libxl-use-EHCI-for-providing-tablet-USB-device.patch
	"02bc6a2feee14cdb40e91a7052f1c87f34b79c442ef483c44d04e5470127cfb47e0069e79aa1c0d063110fe2b568040d3fbc38ccb8fbfeacfe7b7ab7fa53a12a" # 1007-libxl-allow-kernel-cmdline-without-kernel-if-stubdom.patch
	"819b8d3e756d09625261a5ff46c9b7d4003127b4403268a61d2e66238015e3eb33b8adcc67e7e4943933fdb54cecd21d7527900b398b61d5939e4daee4bd70bb" # 1008-libxl-Force-emulating-readonly-disks-as-SCSI.patch
	"983658d2a11de892b84c7d2600fb4b5971d92f054fcef0043e01e5bdeb32c4b57667ddf5b3fa59a29e863b90b71d93f876e3e7a69ad089157b04bdebf09b9482" # 1009-tools-xenconsole-replace-ESC-char-on-xenconsole-outp.patch
	"79e0d8690370f7d455d891694b86fc90078bf1ceecec11fa68563ac9ccaa68b44377139d8442ddccbf29e5eaa6bc241d8583dc190529b807d32c78987966b3b0" # 1010-tools-xenconsole-Don-t-replace-ESC-char-by-default.patch
	"e0097e803f8a5fb249a86af38f888dd1860e8a15317b1bbab198af56efa1423735e9cd91a1931fbbcf07e73ffd3ddecd8f4286f05be150d439e5d7e9812a48bd" # 1014-Additional-support-in-ACPI-builder-to-support-SLIC-a.patch
	"7f058161143baf5aa402f86d78b4199df3455c70b952b8691c05a920181e3bf35df3d614109b2916fb9a5e4e437507c7570f50c417bf3086727d405f323b5ced" # 1016-gnttab-disable-grant-tables-v2-by-default.patch
	"54448175b503ebbcbeb0dac3b13f4e5e1848caee29040fb449049fa0e1c4efe935a1b1fba976fd5026b2ea1e39f46c335064e601fa370f86441b6dc84f01e0f5" # 1018-Fix-IGD-passthrough-with-linux-stubdomain.patch
	"62cb1c6f83ce2b0d602c6e3a8194e43f7f88c44a5666af25bdb1952bb28c974979e1da77cec86f4bf417282655880fdfb198a94d9aea6a165ce96b173f492bd4" # 1301-xenconsoled-install-xenstore-watch-for-all-supported.patch
	"dac3d2f240cd03074c00c6e813132904b7778371b9128e827471e2714e240b5e8095ba3ed901566c467bf9871efb7c02dfa591c1ab45d54db4f81ec1eefa5ecc" # 1302-xenconsoled-add-support-for-consoles-using-state-xen.patch
	"5c796c990192d3f37247edb4861a4b20569c4e3e084404af8f278a356365a3c7ae212896d186ae51aa2b7960c1bd8c45602df0594036eb73fa15577e3bf0832c" # 1303-xenconsoled-make-console_type-use_gnttab-less-confus.patch
	"f0e6381f1204b7741074c5eda0a36a36ea853509d0c7b3824e0bf435e42fcfc299245eb6afb2e57b3408f28fb780e53a83d3033cecfa13a875275985e3480ad0" # 1304-xenconsoled-add-support-for-up-to-3-secondary-consol.patch
	"aaf5c08a47005d914aa69ab7ca89773d25c125f6d3d89d0626c519feee0d0207bb19f6a19e869d0fc4353a403d9680aa01f5213ac6d27327ba749095b0a77590" # 1305-xenconsoled-deduplicate-error-handling.patch
	"b3160b15e9afa712086db2479998d18d44fe540f11fb8d0f0e8c7d3a4e19a7aca3a56f8fcfca4c354d4e88fd4becf18a529e48006ad0792a6c23a49a63e14aa1" # 1306-libxl-use-xenconsoled-even-for-multiple-stubdomain-s.patch

	# Patches taken from XenServer's patchset.
	"1558d337a8181810614c9d110e42c939e57a6860b594f6670e77fcbe1450a984be4cdbcb866ae850bdd57dee2686cd2ab5607315592f18c40f86eedc560ed681" # 0001-x86-vpt-fix-injection-to-remote-vCPU.patch
	"0384b29938499d6c281c269ac1adc016a529cacea926dd81b8feb0db00abe80495eb7be86c9f200f2141a0f6a9f25bf7dc440d7e7789db8fd4357a89caaed83b" # 0002-intel-Workaround-Nehalem-and-Westmere-C-states-bugs.patch
	"1b3b9bcef20fa3257020656cf3268fe216e7b02ec10d39170b2cf0a5b360ad3f23bad99abb769535fb0bdde86528fc2d7b9e109c93f8fe9fc9bdfc710cc4edb8" # 0003-vtd-quirks-Ignore-HP-Gen8-GPU-audio-devices-for-RMRR.patch
	"dfe48466d6fe0166428902b1346b9aecab279f8282c9e87f367f4cf98b2fc21cd9533d3051f575c9a0c9edd6084f0c8491e2341996bfedcc6a4d3f7eb94e48c2" # 0004-pci-Add-PCI-phantom-functions-to-Xen-based-on-PCI-ve.patch
	"129fa7ec5930e98a99c15f1efdc0c23ea508492d95095bf1ed84e2a98b131fc0e8523acafe15e44c257ca6d1217a291e30057533daeb78826c368336d2fd0e61" # 0005-x86-hpet-Pre-cleanup.patch
	"51e95193d3d2a27c9ad7e5fc24a73d88688b7183fee3c037b9f3e8e10ac97427ba869c5c1a5fc6004e5d3d41da66cc63a0c06fe3fd10d6ae4604a744a5d3d776" # 0006-x86-hpet-Use-singe-apic-vector-rather-than-irq_descs.patch
	"1d016e8f8c0e6a76453e21fefb87949a12be24a48c84d1fdac67aea500e7a6b3721399fd8911f73ddfdfffad8831598a4afc8e207fde8a34a3b52e97c4e23478" # 0007-x86-hpet-Post-cleanup.patch
	"ae1a7c9357e77b871040755e55d5ec016c17bf917f7c637a27a944273f58b3ce577934c7c1532874c9515c8456cac4d983f8cb8363524b6b11fbf511593bc780" # 0008-libxc-retry-shadow-ops-if-EBUSY-is-returned.patch
)


_stub_sums=(
	"2397795a0a4999a6efee3d8291356673d1757bc1b34dd2015378ef6ea8800ee1317c7d9f902d82bd62ff8d451223ad51ced5e3a6d66e8e79930a7f513cc2b805" # vtpm-gcc12-fixes.patch
	"d26dca4998807b9910b34e12633da8b347b154740fe9b11a2ee8da72d8e34daf9822f857a10a07effdc533e6d93e04eb95f4ff9a3b7a73ee6a62f2892eff4655" # add-stubdom-fixes.patch
	"2e0b0fd23e6f10742a5517981e5171c6e88b0a93c83da701b296f5c0861d72c19782daab589a7eac3f9032152a0fc7eff7f5362db8fccc4859564a9aa82329cf" # gmp-4.3.2.tar.bz2
	"c2bc9ffc8583aeae71cee9ddcc4418969768d4e3764d47307da54f93981c0109fb07d84b061b3a3628bd00ba4d14a54742bc04848110eb3ae8ca25dbfbaabadb" # grub-0.97.tar.gz
	"1465b58279af1647f909450e394fe002ca165f0ff4a0254bfa9fe0e64316f50facdde2729d79a4e632565b4500cf4d6c74192ac0dd3bc9fe09129bbd67ba089d" # lwip-1.3.0.tar.gz
	"40eb96bbc6736a16b6399e0cdb73e853d0d90b685c967e77899183446664d64570277a633fdafdefc351b46ce210a99115769a1d9f47ac749d7e82837d4d1ac3" # newlib-1.16.0.tar.gz
	"2b3d98d027e46d8c08037366dde6f0781ca03c610ef2b380984639e4ef39899ed8d8b8e4cd9c9dc54df101279b95879bd66bfd4d04ad07fef41e847ea7ae32b5" # pciutils-2.2.9.tar.bz2
	"88da614e4d3f4409c4fd3bb3e44c7587ba051e3fed4e33d526069a67e8180212e1ea22da984656f50e290049f60ddca65383e5983c0f8884f648d71f698303ad" # polarssl-1.1.4-gpl.tgz
	"4928b5b82f57645be9408362706ff2c4d9baa635b21b0d41b1c82930e8c60a759b1ea4fa74d7e6c7cae1b7692d006aa5cb72df0c3b88bf049779aa2b566f9d35" # tpm_emulator-0.7.4.tar.gz
	"021b958fcd0d346c4ba761bcf0cc40f3522de6186cf5a0a6ea34a70504ce9622b1c2626fce40675bc8282cf5f5ade18473656abc38050f72f5d6480507a2106e" # zlib-1.2.3.tar.gz
)


# Simplify things for makepkg
source=( "${_source[@]}" "${_patches[@]}" "${_feature_patches[@]}" )
sha512sums=( "${_sha512sums[@]}" "${_patch_sums[@]}" "${_feature_patch_sums[@]}" )
noextract+=( "${_patches[@]##*/}" "${_feature_patches[@]##*/}" )

# stubdom handling
if [ "${_build_stubdom}" == "true" ]; then
	source=("${source[@]}" "${_stubdom_source[@]}")
	sha512sums=("${sha512sums[@]}" "${_stub_sums[@]}")

	# Add in automagic dependency in order to build vtpm and vtpmmgr stubdoms
	makedepends=( "${makedepends[@]}" "${_stubdom_makedepends[@]}" )

	for file in "${_stubdom_source[@]}"; do
		noextract+=( $(basename ${file}) )
	done

	_config_stubdom='--enable-stubdom'

	# make sure to build the stubdom package
	pkgname+=("xen-stubdom")

else
	_config_stubdom='--disable-stubdom'
fi

# TODO: Setup users, dirs, etc.

prepare() {

	cd "${pkgbase}"

	# This is required for applying patches with git am. Assume no user is set
	# up.
	git config user.name user
	git config user.email user@localhost

	for patchurl in "${_feature_patches[@]}"; do
		patch=$(basename $patchurl)
		git am "../${patch}"
	done

	if [ "${_build_stubdom}" == "true" ]; then

		for file in "${_stubdom_source[@]}"; do
			cp ../$(basename ${file}) stubdom/
		done

		echo "==> Applying GCC 12.1 fixes for stubdom..."
		cp ../vtpm-gcc12-fixes.patch stubdom/
		patch -p1 < ../add-stubdom-fixes.patch
	fi

	for patchurl in "${_patches[@]}"; do
		patch=$(basename $patchurl)
		echo "==> Applying security patch '${patch}'..."
		patch -p1 < "../${patch}"
	done

	# Fix Install Paths.
	sed 's,/var/run,/run,g' -i tools/hotplug/Linux/locking.sh
	sed 's,/var/run,/run,g' -i tools/xenmon/xenbaked.c
	sed 's,/var/run,/run,g' -i tools/xenmon/xenmon.py
	sed 's,/var/run,/run,g' -i tools/pygrub/src/pygrub

	cd xen
	echo "==> Setting config..."
	cp ../../config .config
	make olddefconfig
	diff -u ../../config .config || :
}

pkgver() {
	cd "${srcdir}/${pkgbase}"
	./version.sh --full xen/Makefile | sed 's/-//'
}

build() {
	cd "${pkgbase}"

	if [ "${_build_stubdom}" == "true" ]; then
		echo "NOTE: Xen build with stubdom support."
	fi

	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--with-rundir=/run \
		--enable-systemd \
		--enable-ovmf \
		--disable-qemu-traditional \
		${_config_stubdom} \
		--with-system-qemu=/usr/lib/xen/bin/qemu-system-i386 \
		--with-sysconfig-leaf-dir=conf.d \
		--with-system-seabios=/usr/share/qemu/bios-256k.bin \
		--disable-ocamltools

	make "${_common_make_flags[@]}"
}

package_xen() {
	pkgdesc='Open-source type-1 or baremetal hypervisor'

	depends=(
		'zlib' 'python' 'ncurses' 'openssl' 'libx11' 'libuuid.so' 'yajl' 'libaio' 'glib2' 'pkgconf'
		'bridge-utils' 'iproute2' 'inetutils' 'acpica' 'lib32-glibc' 'gnutls'
		'vde2' 'lzo' 'pciutils' 'sdl2'
		'pixman' 'libseccomp' 'libpng' 'libjpeg-turbo' # inhereted depends because of build environment
	)

	optdepends=(
		'xen-qemu: HVM and PV support'
		'seabios: SeaBIOS payload support'
		'xen-docs: HTML documentation and man pages'
		'grub-xen-git: GRUB and pvgrub2 bootloader support'
		'linux-headers: extract bootable non-zstd kernel for recent kernels'
	)

	install="xen.install"

	backup=(
		"etc/conf.d/xencommons"
		"etc/conf.d/xendomains"
		"etc/xen/efi-xen.cfg"
		"etc/xen/cpupool"
		"etc/xen/xl.conf"
	)

	cd "${pkgbase}"

	make "${_common_make_flags[@]}" DESTDIR="$pkgdir" install

	rm -rf "$pkgdir"/var/run

	#  Symlinks to prior installed versions are not The Arch Way, leave only the bare EFI binary
	(cd "${pkgdir}/${_efi_dir}" && mv "$(realpath xen.efi)" xen.efi)

	[ -d "$pkgdir"/etc/xen/scripts ] && backup+=($(find "$pkgdir"/etc/xen/scripts/ -type f | sed "s|^$pkgdir/||g"))

	mkdir -p "${pkgdir}/var/log/xen/console"

	# Continued: Trim hypervisor symlinks.
	(cd "${pkgdir}/${_boot_dir}" && mv "$(realpath xen.gz)" xen.gz)

	# Do all symlink removals after the directories have had the real
	# binaries moved overtop any symlinks. Note that dependening on
	# configuratation _efi_dir and _boot_dir may be the same directory, so
	# don't clean any of them until they've all been processed.
	find "${pkgdir}/${_efi_dir}" -type l -delete
	find "${pkgdir}/${_boot_dir}" -type l -delete

	# Remove syms.
	find "${pkgdir}/usr/lib/debug" -type f \( -name '*-syms*' -or -name '*\.map' \) -delete
	rmdir "${pkgdir}/usr/lib/debug/usr/lib/xen/boot"
	rmdir "${pkgdir}/usr/lib/debug/usr/lib/xen"
	rmdir "${pkgdir}/usr/lib/debug/usr/lib"
	rmdir "${pkgdir}/usr/lib/debug/usr"
	rmdir "${pkgdir}/usr/lib/debug"

	# Remove SysVinit files.
	rm -r "${pkgdir}/etc/init.d"

	# Install files for Arch Linux.
	install -D -m 0644 "${srcdir}/efi-xen.cfg" "${pkgdir}/etc/xen/efi-xen.cfg"
	install -D -m 0644 "${srcdir}/xen.conf" "${pkgdir}/usr/lib/modules-load.d/xen.conf"
	install -D -m 0644 "${srcdir}/tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgbase}.conf"

	# microcode hooks
	mkdir -p "${pkgdir}/usr/share/libalpm/scripts"     "${pkgdir}/usr/share/libalpm/hooks"
	install -m755 "${srcdir}/xen-ucode-extract.sh"     "${pkgdir}/usr/share/libalpm/scripts"
	install -m644 "${srcdir}/xen-intel-ucode.hook"     "${pkgdir}/usr/share/libalpm/hooks"
	install -m644 "${srcdir}/xen-amd-ucode.hook"       "${pkgdir}/usr/share/libalpm/hooks"

	# Remove documentation (included in separate xen-docs package).
	rm -r "${pkgdir}/usr/share/doc"
	rm -r "${pkgdir}/usr/share/man"

	# remove stubdom files
	rm -f \
		"${pkgdir}/usr/lib/xen/boot/vtpmmgr-stubdom.gz"     \
		"${pkgdir}/usr/lib/xen/boot/vtpm-stubdom.gz"        \
		"${pkgdir}/usr/lib/xen/boot/xenstorepvh-stubdom.gz" \
		"${pkgdir}/usr/lib/xen/boot/xenstore-stubdom.gz"
}

package_xen-docs() {
	pkgdesc="Xen hypervisor documentation and man pages"
	arch=("any")
	cd "${pkgbase}"
	make "${_common_make_flags[@]}" DESTDIR="$pkgdir" install-docs
}

package_xen-stubdom() {
	pkgdesc="Xen hypervisor stubdom files"
	arch=("x86_64")
	depends=("xen")

	cd "${srcdir}/${pkgbase}/stubdom"
	make DESTDIR="${pkgdir}" install
}
