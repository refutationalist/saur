# Maintainer: Sam Mulvey <archlinux@sammulvey.com>

# for build changes, check:
#  https://collaboraonline.github.io/post/build-code/#build-code-on-arch

# tag for this version
_code_tag="cp-25.04.1-1"

## Compile time limitations for open connections and documents
_connections=100
_documents=50

# To save time and considerable resources, we're using the precompiled
# LibreOffice shipped by Collabora. 
_locore_ver="25.04"

# if you change this, change install as well
_locore_location="/usr/lib/code"

# The version number of the debian file which is literally the only place I couild
# find the branding files.
_code_brand_ver="24.04-35"


pkgname=code
pkgver=25.04.1.1
pkgrel=1
pkgdesc="Collabora Online Development Environment - for NextCloud Office"
arch=("x86_64")
url="https://collaboraonline.github.io/"
license=("MPL-2.0")
backup=(
  etc/coolwsd/coolkitconfig.xcu
  etc/coolwsd/coolwsd.xml
)

depends=(
	'libcap' 'libpng' 'poco' 'cpio' 'fontconfig'
	# fonts
	'ttf-liberation' 'ttf-dejavu' 'ttf-carlito'
)
makedepends=(
	'cppunit' 'nodejs' 'npm' 'chromium' 'python-lxml' 'python-polib'
)

install="code.install"
source=(
	"git+https://github.com/CollaboraOnline/online.git#tag=${_code_tag}"
	"https://github.com/CollaboraOnline/online/releases/download/for-code-assets/core-co-${_locore_ver}-assets.tar.gz"
	"https://www.collaboraoffice.com/repos/CollaboraOnline/CODE/code-brand_${_code_brand_ver}_all.deb"
	"code.sysusers"
)

sha256sums=('c92430b16af6b28e9c5b55a19f29f0ccf670cb69279aceef8b72a933715496ff'
            '3888d3a8d303ac86fbb0cfb1c29e8b37d0b6cc13e345dc0b60fd6fb10b139f93'
            '14557257c710d8785e96f78a70d46a11c7adfdf4196716e78efc905242f9599a'
            '1be901b3c452d72b595a5596df802a01f9936e9f12d8e623eacb4b5372bf3f85')



prepare() {
	
	echo "==> NOTE: Building with limits of ${_connections} max connections and ${_documents} max documents"
	echo "==> Running autogen.sh"
	cd "${srcdir}/online"
	./autogen.sh
	echo "==> running configure"

	# setcap is for packaging
	# SSL is disabled since this needs to be behind a reverse proxy
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--runstatedir=/run \
		--with-lokit-path=../include \
		--with-lo-path=${_locore_location} \
		--with-max-connections=${_connections} \
		--with-max-documents=${_documents} \
		--disable-ssl

	echo "==> Unpacking CODE branding files"
	cd "${srcdir}"
	mkdir branding
	cd branding
	ar x ../code-brand_${_code_brand_ver}_all.deb
	tar axf data.tar.xz

}

build() {
	cd "${srcdir}/online"
	make 
}

package() {

	echo "==> putting LibreOffice in the right place"
	mkdir -p "${pkgdir}${_locore_location}"
	cp -R "${srcdir}/instdir/." "${pkgdir}${_locore_location}"

	echo "==> removing built-in fonts in favor of system fonts"
	rm -Rf "${pkgdir}${_locore_location}/share/fonts"
	ln -sf /usr/share/fonts "${pkgdir}${_locore_location}/share/fonts"

	echo "==> running make install"
	cd "${srcdir}/online"
	make DESTDIR="${pkgdir}" install


	echo "==> moving web configuration to the documentation"
	mkdir -p "${pkgdir}/usr/share/doc/coolwsd/example"
	mv "${pkgdir}/etc/apache2" "${pkgdir}/etc/nginx" "${pkgdir}/usr/share/doc/coolwsd/example"

	echo "==> setting up system administration stuff"
	install -Dm0644 "${srcdir}/code.sysusers" "${pkgdir}/usr/lib/sysusers.d/code.conf"

	mkdir -p "${pkgdir}/usr/lib/systemd/system"
	sed -i 's,/opt/cool,/srv/cool,g' coolwsd.service
	install -Dm0644 coolwsd.service "${pkgdir}/usr/lib/systemd/system"

	echo "==> moving branding stuff around"
	# make install uses rsync, so why don't we?
	rsync -av "${srcdir}/branding/opt/collaboraoffice/share/theme_definitions/." "${pkgdir}${_locore_location}/share/theme_definitions/."
	rsync -av "${srcdir}/branding/usr/share/coolwsd/browser/dist/." "${pkgdir}/usr/share/coolwsd/browser/dist/."




}

