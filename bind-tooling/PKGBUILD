# I don't need everything the official package pulls in.  Specifically: python
# Maintainer: Sam Mulvey <code@ktqa.org>
# With help from the official bind PKGBUILD
pkgname=bind-tooling
pkgver=9.20.3
pkgrel=1
pkgdesc="The Arch Way for me, but not for thee.  (dig, host, nslookup)"
arch=("x86_64" "aarch64")
url='https://www.isc.org/software/bind/'
license=('MPL2')
depends=(libxml2 json-c libidn2 libuv lmdb)
conflicts=("bind" "bind-tools")
replaces=("bind-tools")
validpgpkeys=('D99CCEAF879747014F038D63182E23579462EFAA' #Michal Nowak
              '706B6C28620E76F91D11F7DF510A642A06C52CEC') #Michał Kępień

source=("https://ftp.isc.org/isc/bind9/${pkgver}/bind-${pkgver}.tar.xz"{,.asc})
sha256sums=('f90c2da1621299f56a2e6585a6fe459ec3efd6f2fdf84a8fbf31b40be7698a73'
            'SKIP')


build() {
	cd "bind-$pkgver"
	# support to chase DNSSEC signature chains
	CFLAGS+=' -DDIG_SIGCHASE'
	# compile with gcc10, https://gcc.gnu.org/gcc-10/porting_to.html
	CFLAGS+=' -fcommon'
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--localstatedir=/var \
		--disable-static \
		--without-python \
		--with-openssl \
		--with-libidn2 \
		--with-json-c \
		--with-libxml2 \
		--with-libtool
	cd "lib"
	make
	cd "../bin/dig"
	make


}


package() {
	cd "bind-$pkgver"
	install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
	install -Dm644 LICENSE COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/"

	cd "lib"
	make DESTDIR="$pkgdir" install
	rm -Rf "$pkgdir//usr/include"

	cd "../bin/dig/.libs"
	install -Dm755 dig "$pkgdir/usr/bin/dig"
	install -Dm755 host "$pkgdir/usr/bin/host"
	install -Dm755 nslookup "$pkgdir/usr/bin/nslookup"


}
